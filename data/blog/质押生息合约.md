---
title: Solidity实现质押生息
date: '2022-12-25'
tags: ['solidity', 'smart contract','erc721','erc20']
draft: false
summary: 实现质押Token(ERC20或者ERC721)获得代币奖励
---

# ERC20质押生息

在PancakeSwap的Farms和Pools里或者一些别的交易所中我们经常看到能通过质押ERC20 Token获取到额外的ERC20 Token。

这种玩法的开创是当时sushi和uniswap竞争时产生的玩法。

## 原理

所有的用户去维护一个公共变量，当有一个新的用户进场质押导致每个用户的质押比例变化时，它会调用一个函数去更新公共变量，从而调整每个用户的比例

## 实现

Token质押生息需要指定两个ERC20的地址，一个作为质押的代币，一个作为奖励的代币，当然这两者可以是同一个代币合约地址，即奖励的代币和质押的代币为同一个代币。

### 代币合约

* 在openzeppelin上随便找一个erc20合约demo来部署，作为我们质押生息合约的代币。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract TokenA is ERC20,Ownable {
    constructor() ERC20("TokenA", "TA"){}

    function mint(address to,uint256 amount)public onlyOwner{
        _mint(to,amount);
    }
}
```

部署完代币合约后需要做两件事：

1. 给一个账户地址mint代币，之后用这个账户去质押。(mint需要合约部署账户去操作)
2. 执行approve给质押生息合约授权，spender为合约地址，amount为我们质押数量。这一步在质押生息合约部署后再去操作。

### 质押生息合约

选择StakingRewards合约部署，部署时填入上面的代币合约地址

![image-20230203172843695](https://raw.githubusercontent.com/selfmakeit/resource/main/image-20230203172843695.png)

部署之后还需要做以下步骤：

1. 设置挖矿时间，即质押获得奖励的时长，时间为秒，在测试时时长设置短一点。(方法：setRewardsDuration)
2. 转入奖励代币：在奖励代币的合约方法里选择mint方法，地址填写质押生息的合约地址。
3. 设置奖励代币的总数量。(方法：notifyRewardAmount)
4. 在代币合约中执行approve方法给质押生息合约授权。
5. 执行质押生息合约的stake方法去质押。
6. 查看获得的奖励。

质押生息合约代码：

```solidity
// SPDX-License-Identifier: MIT

/*
使用说明
1.设置挖矿时间(质押获得奖励所需的时间)
2.转入rewardtoken
3.设置rewardtoken的总数量
*/
pragma solidity ^0.8;

contract StakingRewards {
    IERC20 public  stakingToken;
    IERC20 public  rewardsToken;

    address public owner;

    // Duration of rewards to be paid out (in seconds)
    uint public duration;
    // Timestamp of when the rewards finish
    uint public finishAt;
    // Minimum of last updated time and reward finish time
    uint public updatedAt;
    // Reward to be paid out per second
    uint public rewardRate;
    // Sum of (reward rate * dt * 1e18 / total supply)
    uint public rewardPerTokenStored;
    // User address => rewardPerTokenStored
    mapping(address => uint) public userRewardPerTokenPaid;
    // User address => rewards to be claimed
    mapping(address => uint) public rewards;

    // Total staked
    uint public totalSupply;
    // User address => staked amount
    mapping(address => uint) public balanceOf;

    constructor(address _stakingToken, address _rewardToken) {
        owner = msg.sender;
        stakingToken = IERC20(_stakingToken);
        rewardsToken = IERC20(_rewardToken);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "not authorized");
        _;
    }

    modifier updateReward(address _account) {
        rewardPerTokenStored = rewardPerToken();
        updatedAt = lastTimeRewardApplicable();

        if (_account != address(0)) {
            rewards[_account] = earned(_account);
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;
        }

        _;
    }

    function lastTimeRewardApplicable() public view returns (uint) {
        return _min(finishAt, block.timestamp);
    }

    function rewardPerToken() public view returns (uint) {
        if (totalSupply == 0) {
            return rewardPerTokenStored;
        }

        return
            rewardPerTokenStored +
            (rewardRate * (lastTimeRewardApplicable() - updatedAt) * 1e18) /
            totalSupply;
    }

    function resetToken(address _stakingToken, address _rewardToken) public onlyOwner{
        stakingToken = IERC20(_stakingToken);
        rewardsToken = IERC20(_rewardToken);

    }

    function stake(uint _amount) external updateReward(msg.sender) {
        require(_amount > 0, "amount = 0");
        stakingToken.transferFrom(msg.sender, address(this), _amount);
        balanceOf[msg.sender] += _amount;
        totalSupply += _amount;
    }

    function withdraw(uint _amount) external updateReward(msg.sender) {
        require(_amount > 0, "amount = 0");
        balanceOf[msg.sender] -= _amount;
        totalSupply -= _amount;
        stakingToken.transfer(msg.sender, _amount);
    }

    function earned(address _account) public view returns (uint) {
        return
            ((balanceOf[_account] *
                (rewardPerToken() - userRewardPerTokenPaid[_account])) / 1e18) +
            rewards[_account];
    }

    function getReward() external updateReward(msg.sender) {
        uint reward = rewards[msg.sender];
        if (reward > 0) {
            rewards[msg.sender] = 0;
            rewardsToken.transfer(msg.sender, reward);
        }
    }
    //设置可以挖矿的时间，单位s
    function setRewardsDuration(uint _duration) external onlyOwner {
        require(finishAt < block.timestamp, "reward duration not finished");
        duration = _duration;
    }
    //可获取的ERC20数量
    function notifyRewardAmount(
        uint _amount
    ) external onlyOwner updateReward(address(0)) {
        if (block.timestamp >= finishAt) {
            rewardRate = _amount / duration;
        } else {
            uint remainingRewards = (finishAt - block.timestamp) * rewardRate;
            rewardRate = (_amount + remainingRewards) / duration;
        }

        require(rewardRate > 0, "reward rate = 0");
        require(
            rewardRate * duration <= rewardsToken.balanceOf(address(this)),
            "reward amount > balance"
        );

        finishAt = block.timestamp + duration;
        updatedAt = block.timestamp;
    }

    function _min(uint x, uint y) private pure returns (uint) {
        return x <= y ? x : y;
    }

    function getBalanceOfContract() public view returns (uint) {
        return rewardsToken.balanceOf(address(this));
    }

    function withdrawRewardToken() external onlyOwner {
        require(block.timestamp >= finishAt, "still product rewardToken");
        rewardsToken.transfer(msg.sender, getBalanceOfContract());
    }

}

interface IERC20 {
    function totalSupply() external view returns (uint);

    function balanceOf(address account) external view returns (uint);

    function transfer(address recipient, uint amount) external returns (bool);

    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint amount) external returns (bool);

    function transferFrom(
        address sender,
        address recipient,
        uint amount
    ) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint value);
    event Approval(address indexed owner, address indexed spender, uint value);
}
```

# NFT质押生息

原理和上面的一致，只是我们质押的资产变成了NFT，获得的资产同样还是ERC20代币。

步骤和上面一样，先编写并部署一个NFT合约，再部署NFT质押生息合约。

NFT合约代码：

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Counters.sol";

contract MyNft is ERC721,Ownable {
    using Counters for Counters.Counter;
    Counters.Counter private _tokenIdCounter; 

    constructor() ERC721("MyNft", "MNFT"){}

    function safeMint(address to)public onlyOwner{
        uint256 tokenId = _tokenIdCounter.current();
        _tokenIdCounter.increment();
        _safeMint(to,tokenId);
    }
}
```

质押生息合约:

```solidity
// SPDX-License-Identifier: MIT

/*
使用说明
1.设置挖矿时间
2.转入rewardtoken
3.设置rewardtoken的总数量
*/
pragma solidity ^0.8;

import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

contract StakingRewards {
    //IERC20 public  stakingToken;
    IERC721 public stakingERC721;
    IERC20 public  rewardsToken;

    address public owner;

    // Duration of rewards to be paid out (in seconds)
    uint public duration;
    // Timestamp of when the rewards finish
    uint public finishAt;
    // Minimum of last updated time and reward finish time
    uint public updatedAt;
    // Reward to be paid out per second
    uint public rewardRate;
    // Sum of (reward rate * dt * 1e18 / total supply)
    uint public rewardPerTokenStored;
    // User address => rewardPerTokenStored

    uint starAt;
    mapping(address => uint) public userRewardPerTokenPaid;
    // User address => rewards to be claimed
    mapping(address => uint) public rewards;

    // Total staked
    uint public totalSupply;
    // User address => staked amount
    mapping(address => uint) public balanceOf;
    //质押NFT address=>tokenId=>isStakeInContract
    mapping(address => mapping(uint => bool)) public NFTStakeIndex;

    uint constant ONE_NFT = 10**18;


    constructor(address _stakingERC721, address _rewardToken) {
        owner = msg.sender;
        //stakingToken = IERC20(_stakingToken);
        stakingERC721 = IERC721(_stakingERC721);
        rewardsToken = IERC20(_rewardToken);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "not authorized");
        _;
    }

    modifier updateReward(address _account) {
        rewardPerTokenStored = rewardPerToken();
        updatedAt = lastTimeRewardApplicable();

        if (_account != address(0)) {
            rewards[_account] = earned(_account);
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;
        }

        _;
    }

    function lastTimeRewardApplicable() public view returns (uint) {
        return _min(finishAt, block.timestamp);
    }

    function rewardPerToken() public view returns (uint) {
        if (totalSupply == 0) {
            return rewardPerTokenStored;
        }

        return
            rewardPerTokenStored +
            (rewardRate * (lastTimeRewardApplicable() - updatedAt) * 1e18) /
            totalSupply;
    }

    function resetToken(address _stakingERC721, address _rewardToken) public onlyOwner{
        stakingERC721 = IERC721(_stakingERC721);
        rewardsToken = IERC20(_rewardToken);

    }

    function stake(uint tokenId) external updateReward(msg.sender) {
        //require(_amount > 0, "amount = 0");
        require(stakingERC721.ownerOf(tokenId) == msg.sender, "not owner of NFT");
        //stakingToken.transferFrom(msg.sender, address(this), _amount);
        stakingERC721.transferFrom(msg.sender, address(this), tokenId);
        balanceOf[msg.sender] += ONE_NFT;
        totalSupply += ONE_NFT;
        NFTStakeIndex[msg.sender][tokenId] = true;//质押NFTindex
    }

    function withdraw(uint tokenId) external updateReward(msg.sender) {
        //require(_amount > 0, "amount = 0");
        require(NFTStakeIndex[msg.sender][tokenId], "NFT not staking");
        balanceOf[msg.sender] -= ONE_NFT;
        totalSupply -= ONE_NFT;
        //stakingToken.transfer(msg.sender, _amount);
        stakingERC721.transferFrom(address(this), msg.sender, tokenId);
    }

    function earned(address _account) public view returns (uint) {
        return
            ((balanceOf[_account] *
                (rewardPerToken() - userRewardPerTokenPaid[_account])) / 1e18) +
            rewards[_account];
    }

    function getReward() external updateReward(msg.sender) {
        uint reward = rewards[msg.sender];
        if (reward > 0) {
            rewards[msg.sender] = 0;
            rewardsToken.transfer(msg.sender, reward);
        }
    }
    //设置可以挖矿的时间，单位s
    function setRewardsDuration(uint _duration) external onlyOwner {
        require(finishAt < block.timestamp, "reward duration not finished");
        duration = _duration;
        starAt = block.timestamp;
    }
    //可获取的ERC20数量
    function notifyRewardAmount(
        uint _amount
    ) external onlyOwner updateReward(address(0)) {
        if (block.timestamp >= finishAt) {
            rewardRate = _amount / duration;
        } else {
            uint remainingRewards = (finishAt - block.timestamp) * rewardRate;
            rewardRate = (_amount + remainingRewards) / duration;
        }

        require(rewardRate > 0, "reward rate = 0");
        require(
            rewardRate * duration <= rewardsToken.balanceOf(address(this)),
            "reward amount > balance"
        );

        finishAt = block.timestamp + duration;
        updatedAt = block.timestamp;
    }

    function _min(uint x, uint y) private pure returns (uint) {
        return x <= y ? x : y;
    }

    function getBalanceOfContract() public view returns (uint) {
        return rewardsToken.balanceOf(address(this));
    }

    function withdrawRewardToken() external onlyOwner {
        require(block.timestamp >= finishAt, "still product rewardToken");
        rewardsToken.transfer(msg.sender, getBalanceOfContract());
    }

    function leftingTime() external view returns(uint){
        if((duration + starAt) >= block.timestamp)
            return duration + starAt - block.timestamp;
        else 
            return 0;
      
    }

    function earnedByUser()public view returns (uint) {
        return earned(msg.sender);
    }

}

interface IERC20 {
    function totalSupply() external view returns (uint);

    function balanceOf(address account) external view returns (uint);

    function transfer(address recipient, uint amount) external returns (bool);

    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint amount) external returns (bool);

    function transferFrom(
        address sender,
        address recipient,
        uint amount
    ) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint value);
    event Approval(address indexed owner, address indexed spender, uint value);
}
```
